/* adder.jj Adding up numbers */
options {
    STATIC = false;
}
PARSER_BEGIN(LabelParser)
package se.uu.it.smbugfinder.encoding.javacc;

import java.util.*;

import se.uu.it.smbugfinder.encoding.*;

class LabelParser extends AbstractLabelParser {
    
}
PARSER_END(LabelParser)

SKIP : { "\n" | "\r" | "\r\n" }



// set operations
TOKEN : { < DIFFERENCE : "-" > }
TOKEN : { < UNION : "U" > }

// we parse the symbol body (e.g. update/guard) in its own lexical state, in order to avoid matches with the very broad token SYMBOL  

//SKIP : { " " }
TOKEN : { < SEP : " " > }

TOKEN : { < UPDATE_START : "[" >: BODY }

// update operators
< BODY > TOKEN : { < ASSIGN : (":=") > }
< BODY > TOKEN : { < UPDATE_SEP : "; " > }
< BODY > TOKEN : { < UPDATE_END : "]" >: DEFAULT }


TOKEN : { < GUARD_START : " where " >: BODY }
//TOKEN : { < DO : "do" > }

< BODY > TOKEN : { < INV_START : ("(") > }
< BODY > TOKEN : { < INV_SEP : ","(" ")+ > }
< BODY > TOKEN : { < INV_END : (")") > }

//logical operators
< BODY > TOKEN : { < AND : ("and") > }
< BODY > TOKEN : { < OR : ("or") > }

// relational operators
< BODY > TOKEN : { < EQUAL : ("==") > }
< BODY > TOKEN : { < NOT_EQUAL : ("!=") > }
< BODY > TOKEN : { < IN : ("in") > }
< BODY > TOKEN : { < NOT_IN : ("!in") > }
< BODY > TOKEN : { < ID : (["a"-"z","A"-"Z","_"])+ > }
< BODY > TOKEN : { < BODY_SEP : " " > }

TOKEN : { < INPUT : "I_"(["a"-"z","A"-"Z","_",",","(",")","0"-"9"])+ > }
TOKEN : { < OUTPUT : "O_"(["a"-"z","A"-"Z","_",",","(",")","0"-"9"])+ > }
TOKEN : { < FILTER : "F_"(["a"-"z","A"-"Z","0"-"9","_",",","(",")","*",".","|","\\","+","!","?"])+ >}
TOKEN : { < OTHER : "other" > }
TOKEN : { < OTHER_INPUT : "other_input" > }
TOKEN : { < OTHER_OUTPUT : "other_output" > }
TOKEN : { < SYMBOL : (["a"-"z","A"-"Z","_",",","(",")","0"-"9"])+ > }


TOKEN : { < ENUM_START : "{" >: ENUM_STATE }
< ENUM_STATE > TOKEN : { < INPUT_ELEMENT : "I_"(["a"-"z","A"-"Z","_",",","(",")","0"-"9"])+["a"-"z","A"-"Z","_","(",")","0"-"9"] >: ENUM_STATE } 
< ENUM_STATE > TOKEN : { < OUTPUT_ELEMENT : "O_"(["a"-"z","A"-"Z","_",",","(",")","0"-"9"])+["a"-"z","A"-"Z","_","(",")","0"-"9"] >: ENUM_STATE }
< ENUM_STATE > TOKEN : { < SYMBOL_ELEMENT : (["a"-"z","A"-"Z","_",",","(",")","0"-"9"])+["a"-"z","A"-"Z","_","(",")","0"-"9"] >: ENUM_STATE }
< ENUM_STATE > TOKEN : { < ENUM_SEP : ","(" ")+ > }
< ENUM_STATE > TOKEN : { < ENUM_END : "}" >: DEFAULT }

Label label():
{
    DescriptionToken description;
    Guard guard = Guard.trueGuard();
    BooleanExpression expr;
    Update update = Update.emptyUpdate();
}
{
    (
        description = description()
        (
            [
                < UPDATE_START >
                  (
                      LOOKAHEAD(2)
                    (
                        
                        update = update()
                    ) |
                    (
                        
                        expr = restrictedExpression()
                        {
                            guard = new Guard(expr);
                        }
                    )
                  )  
                < UPDATE_END >
            ]
            [
                < GUARD_START >
                guard = guard()
            ]
        )
    )
    < EOF >
    {
        return new Label(description, guard, update);
    }
}

Update update():
{
     List<Assignment> assignments = new ArrayList<Assignment >();
     Assignment assignment;
     Update update; 
}
{
    assignment = assignment()
    {
        assignments.add(assignment);
    }
    (
        
        < UPDATE_SEP >
        assignment = assignment()
        {
            assignments.add(assignment);
        }            
    )*
    {
        update = new Update(assignments);
        return update;
    }
}


BooleanExpression restrictedExpression():
{
    Token fieldToken, valToken;
}
{
    fieldToken = < ID >
    < EQUAL >
    valToken = < ID >
    {
        return new RelationalExpression(resolveField(fieldToken.image),  RelationalOperator.EQUAL, resolveConstant(valToken.image) );
    }
}

Assignment assignment():
{
    Token varToken, fieldToken;
}
{
    varToken = < ID >
    < ASSIGN >
    fieldToken = < ID >
    {
        return new Assignment(resolveVariable(varToken.image), resolveField(fieldToken.image) );
    }
}

//Guard earlyGuard():
//{
//    Token fieldToken, constToken;
//}
//{
//    fieldToken = < ID >
//    < EQUAL >
//    constToken = < ID >
//    {
//        BinaryBooleanExpression expr = new BinaryBooleanExpression(
//        return new Assignment(resolveVariable(varToken.image), resolveField(fieldToken.image) );
//    }
//}

Guard guard():
{
    BooleanExpression expr;
}
{
    expr = booleanExpression()
    {
        return new Guard(expr);
    }
}

BooleanExpression booleanExpression():
{
    BooleanExpression left, right;
    LogicalOperator op;
}
{
    (
        left = relationalExpression()
        (
            < BODY_SEP >
            (
                (
                    < AND >
                    { op = LogicalOperator.AND; }
                ) |
                (
                    < OR >
                    { op = LogicalOperator.OR; }
                )
            )
            < BODY_SEP >
            right = relationalExpression()
            {
                left = new BinaryBooleanExpression(left, op, right);
            }
        )* 
    )
    {
        return left;
    }
}

RelationalExpression relationalExpression():
{
    ValueExpression left = null, right = null;
    RelationalOperator op;
}
{
    (
        left = valueExpression()
        (
            < BODY_SEP >
            (
                (
                    < EQUAL >
                    { op = RelationalOperator.EQUAL; }
                ) |
                (
                    < NOT_EQUAL >
                    { op = RelationalOperator.NOT_EQUAL; }
                ) |
                (
                    < IN >
                    { op = RelationalOperator.IN; }
                ) |
                (
                    < NOT_IN >
                    { op = RelationalOperator.NOT_IN; }
                )
            )
            < BODY_SEP >
        ) 
        right = valueExpression() 
    )
    {
        return new RelationalExpression(left, op, right);
    }
}

ValueExpression valueExpression():
{
    Token idToken, argToken;
    ValueExpression expression = null, argument = null;
}
{
    idToken = < ID >
    [
        < INV_START >
        { 
            java.util.List<ValueExpression> arguments = new java.util.ArrayList<ValueExpression>();
        }
        [
            argument = valueExpression()
            //argToken = < ID >
            {
                //arguments.add(new Variable(argToken.image));
                arguments.add(argument);
            }
            (
                < INV_SEP >
//                argToken = < ID >
//                {
//                    arguments.add(new Variable(argToken.image));
//                }
                argument = valueExpression()
                {
                    arguments.add(argument);
                }    
            )*
        ]
        < INV_END >
        {
            Function fun = resolveFunction(idToken.image);
            expression = new FunctionInvocation(fun, arguments);
        }
    ]
    {
        if (expression == null) {
            expression = resolveValueExpression(idToken.image);
        }        
        return expression;
    }
}

FunctionInvocation invocation():
{
    Token idToken;
    Function fun;
    List<ValueExpression> parameters = new ArrayList<ValueExpression >(); 
}
{
    idToken = < ID >
    {
        fun = resolveFunction(idToken.image);
    }
    [
        < INV_START >
        [
            idToken = < ID >
            {
                parameters.add(new Variable(idToken.image));
            }
            (
                < INV_SEP >
                idToken = < ID >
                {
                    parameters.add(new Variable(idToken.image));
                }    
            )*
        ]
        < INV_END >
    ]
    {
        return new FunctionInvocation(fun, parameters);
    }
}

DescriptionToken description():
{
    DescriptionToken left = null;
    SetOperator op = null;
    DescriptionToken right = null;
}
{
    
    ( left = enumtoken() |
      left = regex() |
      left = other() |
      left = symbol()
    )
    (
        LOOKAHEAD(2) 
        < SEP >
        (
            op = setOperator()
        )
        < SEP >
        (
            right = regex() |
            right = enumtoken() |
            right = symbol()
            
        )
        {
            left = new SetExpressionToken(left, op, right);
        }
    )*
    {
       return left;
    }
}

SymbolToken symbol():
{
    SymbolToken s;
}
{
    (
        s = input() |
        s = output() |
        s = anySymbol()
    )
    {
        return s;
    }
}

SymbolToken input():
{
    Token msgToken;
}
{
    msgToken = < INPUT >
    {
        return new SymbolToken(true, msgToken.image.substring(2));
    }
}

SymbolToken output():
{
    Token msgToken;
}
{
    msgToken = < OUTPUT >
    {
        return new SymbolToken(false, msgToken.image.substring(2));
    }
}

SymbolToken anySymbol():
{
    Token msgToken;
}
{
    msgToken = < SYMBOL >
    {
        return new SymbolToken(msgToken.image);
    }
}

RegexToken regex():
{
    Token regexToken;
}
{
    regexToken = < FILTER >
    {
        return new RegexToken(regexToken.image.substring(2)); 
    }
}

OtherToken other():
{
    OtherTokenType otherType = null;
}
{
    (
        (
            < OTHER >
        { 
            otherType = OtherTokenType.ALL;
        }
        ) | (
            < OTHER_INPUT >
        { 
            otherType = OtherTokenType.INPUT;
        }
        ) | (
            < OTHER_OUTPUT >
        { 
            otherType = OtherTokenType.OUTPUT;
        }
        ) 
    )
    {
        return new OtherToken(otherType);
    }
}

EnumerationToken enumtoken():
{
    java.util.List<SymbolToken> symbols = new java.util.ArrayList<SymbolToken>();
    SymbolToken symbol;
}
{
    < ENUM_START >
    symbol=element()
    {
        symbols.add(symbol);
    }
    (
        < ENUM_SEP >
        symbol = element()
        {
            symbols.add(symbol);
        }
    )*
    < ENUM_END >
    {
        return new EnumerationToken(symbols);
    }
}

SymbolToken element():
{
    Boolean input;
    Token msgToken = null;
    String symbolString = null;
}
{   
    (
        (
            msgToken = < INPUT_ELEMENT >
            { input = true; symbolString = msgToken.image.substring(2); }
        ) | (
            msgToken = < OUTPUT_ELEMENT >
            { input = false; symbolString = msgToken.image.substring(2); }
        ) | (
            msgToken = < SYMBOL_ELEMENT >
            { input = null; symbolString = msgToken.image; }
        )
    ) 
    {
        return new SymbolToken(input, symbolString);
    }
}

SetOperator setOperator():
{
    SetOperator op;
}
{
    (
        (
            < DIFFERENCE >
            { op = SetOperator.DIFFERENCE; }
        ) | (
            < UNION >
            { op = SetOperator.UNION; }
        )
    )
    {
        return op;
    }
}
